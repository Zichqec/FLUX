OnMouseMove
{
	if reference3 == 0 //reference3 is what character the mouse is over. 0 is Sakura, 1 is Kero
	{
		MouseMoveSakura
	}
	elseif ISFUNC("OnCustom_MouseMoveExtra") && CustomFuncs[0] == 1; OnCustom_MouseMoveExtra
}

//stroke is an array here, to save on variables. stroke[0] is the original stroke value, which resets after every successful pet. stroke[1] is the continuous stroking. It's the same as stroke[0], but it does not reset until your mouse leaves the character. stroke[2] is the number of successful pets in a row. I haven't used it here but you can if you want.
MouseMoveSakura
{
	if reference4 == "stroke_p"
	{
		stroke[0]++ //Stroke value only goes up when over the appropriate areas
		stroke[1]++ //For continuous petting, which I have removed

		if stroke[0] % 40 == 0
		{
			stroke[0] = 0 //Stroke value is reset
			stroke[2]++
			totalpets++

			if ISFUNC("OnCustom_Petting") && CustomFuncs[0] == 1; OnCustom_Petting
			else; "\0\b[0]\s[1].\w5 .\w5 .\w5\_w[4000]\![raise,OnSurfaceRestore]"
		}
	}
	elseif ISFUNC("OnCustom_MouseMoveSakura") && CustomFuncs[0] == 1; OnCustom_MouseMoveSakura
}

OnMouseLeaveAll //When the mouse is no longer on a hitbox. You can change to OnMouseLeave if you want it to happen when you leave any hitbox for another one
{
	stroke[0] = 0
	stroke[1] = 0
	stroke[2] = 0
}

OnMouseDoubleClick
{
	if reference5 == 0
	{
		if reference3 == 0 //this is the character, as above
		{
			if reference4 == "" || reference4 == "menu"; OnSakuraMenu
			else; MusicPlayer.OnMouseDoubleClick
		}
		elseif ISFUNC("OnCustom_MouseDoubleClickExtra") && CustomFuncs[0] == 1; OnCustom_MouseDoubleClickExtra
	}
}