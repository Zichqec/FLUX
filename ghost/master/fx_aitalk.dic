OnAiTalk
{
	if talkoption == 0 || (talkoption == 1 && Singing) || (talkoption == 2 && !Singing) || reference0 == "Hotkey" || reference0 == "Menu"
	{
		if CHAIN.IDName == ""
		{
			lastTalk = RandomTalk
		}
		else
		{
			lastTalk = ChainTalk
		}
		
		lastTalk
	}
}

RandomTalk
{
	if ISFUNC("OnCustom_RandomTalk") && CustomFuncs[0] == 1
	{
		if SHIORI3FW.BalloonName == "Notes"; "\b[0]"
		--
		OnCustom_RandomTalk
	}
}

OnMinuteChange
{
	if hour == 0 && minute == 0 //Resetting auto updates
	{
		AutoUpdates[1] = 0
		AutoUpdates[3] = 0
		AutoUpdates[4] = 0
		AutoUpdates[5] = 0
	}
	
	if (minute == 0 && hour == 0 && day == 1 && month == 1) && (ISFUNC("OnCustom_NewYear") && CustomFuncs[0] == 1) //Even if you've got hour notifs off, if there's custom dialogue for the new year it'll display
	{
		OnCustom_NewYear 
	}
	elseif minute == 0 && HourNotifs[0] == 1 //Top of the hour notifications
	{
		"\0\b[0]"
		--
		if language == "Placeholder" //TRANSLATORS: Top of the hour notifications, as well as a new years message
		{
			
		}
		elseif language == "Deutsch"
		{
			if hour == 0; "[Es ist Mitternacht.]"
			elseif hour == 12; "[Es ist Mittag.]"
			else; "[Es ist %(hour12) Uhr.]"
		}
		elseif language == "Русский"
		{
			if hour == 0; "[Это полночь.]"
			elseif hour == 12; "[Это полдень.]" //я не знаю если это ж слов или нет
			else; "[Это %(hour12) часов.]" //? часы?
		}
		elseif language == "Français"
		{
			if hour == 0; "[Il est minuit.]"
			elseif hour == 12; "[Il est midi.]"
			else; "[Il est %(hour12) heure.]"
		}
		elseif language == "Polski"
		{
			if hour == 0; "[Jest północ.]"
			elseif hour == 12; "[Jest połódnie.]"
			else; "[jest %(hour12) %(ranoorsmth).]"
		}
		else //English
		{
			if hour == 0; "[It's midnight.]"
			elseif hour == 12; "[It's noon.]"
			else; "[It's %(hour12) o'clock.]"
		}
	}
	else //Won't do offscreen dialogue or update checks if the top of the hour notif happens instead
	{
		if AutoUpdates[0] == 1 && AutoUpdates[1] == 0 //Once a day the ghost will check for updates if the user has auto updates on
		{
			AutoUpdates[1] = 1
			AutoUpdates[3] = 0
			AutoUpdates[4] = 0
			AutoUpdates[5] = 0
			"\![update,ghost,checkonly]"
		}
	}
}

OnSecondChange
{
	if runtime[2] >= 59
	{
		runtime[2] = 0
		runtime[1]++
		if runtime[1] >= 59
		{
			runtime[1] = 0
			runtime[0]++
			totalhours++
		}
	}
	else
	{
		runtime[2]++
	}
	if MusicPlayer.Singing
	{
		MusicPlayer.Playtime++
	}
	if MusicPlayer.DiscordDisplayFlag == 2 && SHIORI3FW.CanTalk //If this ghost was chosen in the Discord plugin, and that hasn't been handled yet
	{
		MusicPlayer.DiscordDisplayFlag = 1
		if MusicPlayer.BalloonIsOpen; "\C"
		--
		"\![raise,OnMusicPlayer.DiscordPluginBoot]"
	}
	--
	if SHIORI3FW.CanTalk //If not talking
	{
		SinceTalk++
	}
	else
	{
		SinceTalk = 0
	}
	if InstalledShellName != "" //If the user installed a shell that's supposed to be only FLELE compatible
	{
		void OnEmptyTempFolder
		ERASEVAR("DraggedFile")
		InstalledShellDialogue
	}
}

InstalledShellDialogue : all
{
	"\0\s[0]\b[0]\*"
	
	if language == "Placeholder" //TRANSLATORS: After installing a new shell. This one happens when FLUX uses my workaround to install FLELE shells that are set up to only be installed by FLELE. It should match the normal shell installed dialogue, except for the envelope used. InstalledShellName will be the name of the shell.
	{
		
	}
	elseif language == "Deutsch"
	{
		"['%(InstalledShellName)' erfolgreich installiert.]\n\n"
		
		"\![*]\q[Zu %(InstalledShellName) wechseln,OnChangeToShell,%(InstalledShellName)]\n\n"
		"\![*]\q[Schließen,OnCancel]"
	}
	elseif language == "Русский"
	{
		"[Удалось Установить '%(InstalledShellName)'.]\n\n"
		
		"\![*]\q[Сменить в %(InstalledShellName),OnChangeToShell,%(InstalledShellName)]\n\n"
		"\![*]\q[Закрыть,OnCancel]"
	}
	elseif language == "Français"
	{
		"\*['%(InstalledShellName)' installé avec succès.]\n\n"
		
		"\![*]\q[Changer pour %(InstalledShellName),OnChangeToShell,%(InstalledShellName)]\n\n"
		"\![*]\q[Fermer,OnCancel]"
	}
	elseif language == "Polski"
	{
		"[Zainstalowałem '%(InstalledShellName)'.]\n\n"
		
		"\![*]\q[Zmień na %(InstalledShellName),OnChangeToShell,%(InstalledShellName)]\n\n"
		"\![*]\q[Zamknij,OnCancel]"
	}
	else //English
	{
		"[Successfully installed '%(InstalledShellName)'.]\n\n"
		
		"\![*]\q[Change to %(InstalledShellName),OnChangeToShell,%(InstalledShellName)]\n\n"
		"\![*]\q[Close,OnCancel]"
	}
	
	"\![raise,OnInstalledNewShell]\e"
}

OnInstalledNewShell
{
	InstalledShellName = ""
}

OnOverlap
{
	if "0-1" _in_ reference0 || "1-0" _in_ reference0
	{
		if ISFUNC("OnCustom_Overlap"); OnCustom_Overlap
	}
	else
	{
		if ISFUNC("OnCustom_OverlapExtra"); OnCustom_OverlapExtra
	}
}

OnSurfaceRestore //OnSurfaceRestore happens around 30 seconds after the ghost's last dialogue. reference0 here stores the number of the previous surface, so you can have them comment when they change back if you like.
{
	if Singing; SingingSurfaces //If a song is playing
	else; NonSingingSurfaces //If no song is playing
}

NonSingingSurfaces
{
	if ISFUNC("OnCustom_IdleSurfaces") && CustomFuncs[0] == 1; REPLACE(OnCustom_IdleSurfaces,"\e","")
	else
	{
		if SHIORI3FW.IsVisible[1] != 0; "\1\s[-1]"
		--
		if SHIORI3FW.LastSurface[0] != 0 || SHIORI3FW.Eventid == "OnWindowStateRestore"; "\0\s[0]"
	}
}

SingingSurfaces
{
	if ISFUNC("OnCustom_SingingSurfaces") && CustomFuncs[0] == 1; REPLACE(OnCustom_SingingSurfaces,"\e","")
	else
	{
		if SHIORI3FW.IsVisible[1] != 0; "\1\s[-1]"
		--
		if SHIORI3FW.LastSurface[0] != 25 || SHIORI3FW.Eventid == "OnWindowStateRestore" || SHIORI3FW.Eventid == "OnShellChanged" || SHIORI3FW.Eventid == "OnGetCoords2"; "\0\s[25]"
	}
}