//I put anything related to who the user is in here. Name, pronouns, birthday.

OnChangeName
{
	"\C\![open,inputbox,OnSetName,-1,""%(username)"",__INPUT_BOX_COLOR__]"
}

OnSetName
{
	_name = reference0
	if CUTSPACE(_name) != ""
	{
		nickname = SHIORI3FW.EscapeAllTags(_name)
	}
	OnConfigMenu
}

OnChangePronoun : all
{
	"\0\b[4]\_q"
	
	if language == "Placeholder" //TRANSLATORS: If the pronoun system needs something extra in your language, do let me know and we can try to work something out. It's important the user be able to change these, though, because most shells probably won't get translated and may rely on these envelopes.
	{
		
	}
	elseif language == "Español"  //%(élella)/%(susu)/%(estáestá)
	{
		"[¿Cómo quieres qué te mencionen?]"
		--
		"\n\n\_q"
		"\![*]\q[Él/Él/Su/,OnPronouns,masc]\n"
		"\![*]\q[Ella/Ella/Su,OnPronouns,fem]\n"
		"\![*]\q[Elle/Elle/Su,OnPronouns,undef]"
		
		"\n\n\![*]\q[Cancelar,OnConfigMenu]\e"
	}
	elseif language == "Deutsch"  //%(ersie)/%(ihmihr)/%(ihnsie)/%(seinihr)
	{
		"[Welche Pronomen möchtest du verwenden?]"
		
		"\n\n\_q"
		"\![*]\q[er/ihm/ihn/sein,OnPronouns,masc]\n"
		"\![*]\q[sie/ihr/sie/ihr,OnPronouns,fem]\n"
		"\![*]\q[dey/denen/demm/deren,OnPronouns,undef]"
		
		"\n\n\![*]\q[Abbrechen,OnConfigMenu]\e"
	}
	elseif language == "Русский" //%(онона)/%(егоее)
	{
		"[Как твои местоименение?]"
		
		"\n\n\_q"
		"\![*]\q[Он/Его/Его,OnPronouns,masc]\n"
		"\![*]\q[Она/Ее/Ее,OnPronouns,fem]\n"
		"\![*]\q[Они/Им/Их,OnPronouns,undef]"
		
		"\n\n\![*]\q[Отменить,OnConfigMenu]\e"
	}
	elseif language == "Français" //%(Capitalize(ilelle))/%(Capitalize(luielle))/%(Capitalize(celuicelle))
	{
		"[Comment veux-tu qu'on s'adresse à toi ?]"
		
		"\n\n\_q"
		"\![*]\q[Il/Lui/Celui,OnPronouns,masc]\n"
		"\![*]\q[Elle/Elle/Celle,OnPronouns,fem]\n"
		"\![*]\q[Neutre (Il/Lui),OnPronouns,undef]\n"
		"\![*]\q[Neutre (Elle/Elle),OnPronouns,undef-fem]"
		
		"\n\n\![*]\q[Annuler,OnConfigMenu]\e"
	}
	elseif language == "Polski" //%(Capitalize(onona))/%(Capitalize(jejjemu))/%(Capitalize(jejjego))
	{
		"[Jak chcesz być nazywany?]"
		
		"\n\n\_q"
		"\![*]\q[On/Jemu/Jego,OnPronouns,masc]\n"
		"\![*]\q[Ona/Jej/Jej,OnPronouns,fem]\n"
		"\![*]\q[Oni/Im/Ich,OnPronouns,undef]"
		
		"\n\n\![*]\q[Anuluj,OnConfigMenu]\e"
	}
	else //English
	{
		"[How would you like to be referred to?]"
		
		"\n\n\_q"
		"\![*]\q[He/Him/His,OnPronouns,masc]\n"
		"\![*]\q[She/Her/Hers,OnPronouns,fem]\n"
		"\![*]\q[They/Them/Their,OnPronouns,undef]"
		--
		"\n\n\![*]\q[Cancel,OnConfigMenu]\e"
	}
}


OnPronouns
{
	gender = reference0
	OnConfigMenu
}

OnNotifyUserInfo
{
	_oldbirthday = birthday
	
	//Account for the old format before v2.0.0 being an array
	if GETTYPEEX(_oldbirthday) != 3; _oldbirthday = _oldbirthday[0]
	
	_month = TOINT(reference2[1])
	_day = TOINT(reference2[2])
	
	birthday = MonthName(_month) + " " + NumSuffix(_day)
	
	if _oldbirthday != birthday && (_month == month && _day == day)
	{
		//DEPRECATED
		//This is here *purely* because some shells made use of it and I want to preserve that content, it is DEPRECATED and may be restricted to use by those shells in the future
		Latch.BirthdayTodayComment = 1
	}
}

MonthName
{
	switch _argv[0] - 1
	{
		"January"
		"February"
		"March"
		"April"
		"May"
		"June"
		"July"
		"August"
		"September"
		"October"
		"November"
		"December"
	}
}

//Sets the appropriate suffix for whatever number you pass in
//Example: 2 → 2nd, 11 → 11th, 123 → 123rd
NumSuffix : all
{
	_str = TOSTR(_argv[0])
	_last = SUBSTR(_str,STRLEN(_str) - 1,1)
	_last2 = SUBSTR(_str,STRLEN(_str) - 2,2)
	CVINT(_last)
	CVINT(_last2)
	
	_str //Output the number, suffixes added below
	
	//Handle teen numbers
	if _last2 >= 11 && _last2 <= 13
	{
		"th"
		return
	}
	
	//Everything else
	case _last
	{
		when 1; "st"
		when 2; "nd"
		when 3; "rd"
		others; "th"
	}
}